# Maintainer: Tales A. Mendon√ßa <talesam@gmail.com>

pkgname=gnome-shell-extension-copyous
pkgdesc="Modern clipboard manager extension for GNOME Shell (full rewrite of Pano)"
pkgver=$(date +%y.%m.%d)
pkgrel=$(date +%H%M)
epoch=1
arch=('any')
license=('GPL-3.0-or-later')
url="https://github.com/boerdereinar/copyous"
depends=('gnome-shell>=45'
         'libgda6'
         'gsound')
makedepends=('gobject-introspection'
             'pnpm'
             'typescript'
             'sass'
             'appstream'
             'glib2'
             'make')
optdepends=('wl-clipboard: Wayland clipboard support')
provides=("$pkgname")
source=("git+${url}.git")
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/copyous"

  # Clone with submodules if not already done
  git submodule update --init --recursive

  # Install dependencies
  pnpm install
}

build() {
  cd "${srcdir}/copyous"

  # Build the extension using Make
  make
}

package() {
  cd "${srcdir}/copyous"

  local uuid="copyous@boerdereinar.dev"
  local schema="${uuid}.gschema.xml"
  local destdir="${pkgdir}/usr/share/gnome-shell/extensions/${uuid}"

  install -d "${destdir}"

  # Extract the built extension zip
  bsdtar xvf "dist/${uuid}.zip" -C "${destdir}/"

  # Compile schemas in the extension directory
  if [ -f "${destdir}/schemas/${schema}" ]; then
    glib-compile-schemas "${destdir}/schemas/"
  fi

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
